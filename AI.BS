// If ally health is low and have heal spell, heal them
IF
	ActionListEmpty()
	HaveSpell(CLERIC_CURE_LIGHT_WOUNDS)
	HPPercentLT(MostDamagedOf(Myself),80)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,12138)
		Spell(MostDamagedOf(Myself),CLERIC_CURE_LIGHT_WOUNDS)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_CURE_MEDIUM_WOUNDS)
	HPPercentLT(MostDamagedOf(Myself),80)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,12138)
		Spell(MostDamagedOf(Myself),CLERIC_CURE_MEDIUM_WOUNDS)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_CURE_SERIOUS_WOUNDS)
	HPPercentLT(MostDamagedOf(Myself),75)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,12138)
		Spell(MostDamagedOf(Myself),CLERIC_CURE_SERIOUS_WOUNDS)
END

// If health is low use a healing potion
IF 
	ActionListEmpty()
	HPPercentLT(Myself,30) // If health is low use a potion
	HasItem("potn08",Myself)  // Potion of Healing
THEN
	RESPONSE #100
		DisplayStringHead(Myself,6990)
		UseItem("potn08",Myself)  // Potion of Healing
END

IF
	ActionListEmpty()
	HPPercentLT(Myself,30)
	HasItem("potn52",Myself)  // Potion of Extra Healing
THEN
	RESPONSE #100
		DisplayStringHead(Myself,6990)
		UseItem("potn52",Myself)  // Potion of Extra Healing
END

// If poisoned use an antidote potion
IF 
	ActionListEmpty()
	StateCheck(Myself,STATE_POISONED) 
	HasItem("potn20",Myself)  // Antidote
THEN
	RESPONSE #100
		DisplayStringHead(Myself,7029)
		UseItem("potn20",Myself)  // Antidote
END

// if diseased use cure disease potion
IF 
	ActionListEmpty()
	StateCheck(Myself,STATE_DISEASED) 
	HasItem("potn17",Myself)  // Elixir of health - cures disease and poisons
THEN
	RESPONSE #100
		DisplayStringHead(Myself,26193)
		UseItem("potn17",Myself)  // Elixir of health - cures disease and poisons
END

// Move out of the way of allies
IF 
	PersonalSpaceDistance(NearestAllyOf(Myself),1)
THEN
	RESPONSE #25
		MoveToOffset([1.1])
	RESPONSE #25
		MoveToOffset([1.-1])
	RESPONSE #25
		MoveToOffset([-1.1])
	RESPONSE #25
		MoveToOffset([-1.-1])
END

// Run away from nearby enemies if health is low
IF 
	ActionListEmpty()
	HPPercentLT(Myself,60)
	See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),5)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,2914)
		RunAwayFrom(NearestEnemyOf(Myself),10)
END

IF 
	ActionListEmpty()
	HPPercentLT(Myself,60)
	See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),10)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,2914)
		RunAwayFrom(NearestEnemyOf(Myself),5)
END

// Debuff spells
IF
	ActionListEmpty()
	HaveSpell(CLERIC_SILENCE_15_FOOT)  // SPPR211.SPL (Silence, 15' Radius)
	OR(2)
		See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
		See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
	!StateCheck(LastSeenBy(Myself),STATE_SILENCED)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,17384)
		Spell(LastSeenBy(Myself),CLERIC_SILENCE_15_FOOT)  // SPPR211.SPL (Silence, 15' Radius)
END

IF
	ActionListEmpty()
	HaveSpell(WIZARD_HORROR)
	NumCreatureVsPartyGT(Myself,4)
	!StateCheck(NearestEnemyOf(Myself),STATE_PANIC)
	!StateCheck(SecondNearestEnemyOf(Myself),STATE_PANIC)
	!StateCheck(ThirdNearestEnemyOf(Myself),STATE_PANIC)
	!StateCheck(FourthNearestEnemyOf(Myself),STATE_PANIC)
	!StateCheck(FifthNearestEnemyOf(Myself),STATE_PANIC)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		DisplayStringHead(Myself,17384)
		Spell(LastSeenBy(Myself),WIZARD_HORROR)
END

// Remove Magic = Reveal Magic
IF
	ActionListEmpty()
	HaveSpell(WIZARD_REVEAL_MAGIC)
	OR(2)
		See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
		See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
	OR(5)
		CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_SHIELD)
		CheckStat(LastSeenBy(Myself),1,SHIELDGLOBE)
		CheckStat(LastSeenBy(Myself),1,MINORGLOBE)
		CheckSpellState(LastSeenBy(Myself),WIZARD_SHIELD)
		CheckSpellState(LastSeenBy(Myself),SPELL_SHIELD)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,17384)
		Spell(LastSeenBy(Myself),WIZARD_REVEAL_MAGIC)
END

IF
	ActionListEmpty()
	HaveSpell(WIZARD_DISPEL_MAGIC)
	OR(2)
		See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
		See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
	OR(5)
		CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_SHIELD)
		CheckStat(LastSeenBy(Myself),1,SHIELDGLOBE)
		CheckStat(LastSeenBy(Myself),1,MINORGLOBE)
		CheckSpellState(LastSeenBy(Myself),WIZARD_SHIELD)
		CheckSpellState(LastSeenBy(Myself),SPELL_SHIELD)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,17384)
		Spell(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_DISPEL_MAGIC)
	OR(2)
		See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
		See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
	OR(5)
		CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_SHIELD)
		CheckStat(LastSeenBy(Myself),1,SHIELDGLOBE)
		CheckStat(LastSeenBy(Myself),1,MINORGLOBE)
		CheckSpellState(LastSeenBy(Myself),WIZARD_SHIELD)
		CheckSpellState(LastSeenBy(Myself),SPELL_SHIELD)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,17384)
		Spell(LastSeenBy(Myself),CLERIC_DISPEL_MAGIC)
END

IF
	ActionListEmpty()
	HaveSpell(WIZARD_TRUE_DISPEL_MAGIC)
	OR(2)
		See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
		See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
	OR(5)
		CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_SHIELD)
		CheckStat(LastSeenBy(Myself),1,SHIELDGLOBE)
		CheckStat(LastSeenBy(Myself),1,MINORGLOBE)
		CheckSpellState(LastSeenBy(Myself),WIZARD_SHIELD)
		CheckSpellState(LastSeenBy(Myself),SPELL_SHIELD)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,17384)
		Spell(LastSeenBy(Myself),WIZARD_TRUE_DISPEL_MAGIC)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_MISCAST_MAGIC)
	OR(2)
		See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
		See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
	!CheckSpellState(LastSeenBy(Myself),MISCAST_MAGIC)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,17384)
		Spell(LastSeenBy(Myself),CLERIC_MISCAST_MAGIC)
END

// Summon spells
IF
	ActionListEmpty()
	HaveSpell(CLERIC_ANIMATE_DEAD)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25956)
		Spell(Myself,CLERIC_ANIMATE_DEAD)
END

// If have a buff spell use it
IF
	ActionListEmpty()
	HaveSpell(CLERIC_BLESS)  // SPPR101.SPL (Bless)
	See(NearestEnemyOf(MySelf))
	!StateCheck(Myself,STATE_BLESS)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25956)
		Spell(Myself,CLERIC_BLESS)  // SPPR101.SPL (Bless)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_CHANT)  // SPPR203.SPL (Chant)
	See(NearestEnemyOf(MySelf))
	!StateCheck(Myself,STATE_CHANT)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25956)
		Spell(Myself,CLERIC_CHANT)  // SPPR203.SPL (Chant)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_PROTECT_FROM_EVIL)
	!CheckSpellState(PartySlot2,PROTECTION_FROM_EVIL)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25956)
		Spell(PartySlot2,CLERIC_PROTECT_FROM_EVIL)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_BARKSKIN)
	!CheckSpellState(PartySlot2,BARKSKIN)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25956)
		Spell(PartySlot2,CLERIC_BARKSKIN)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_AID)
	!CheckSpellState(PartySlot2,AID)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25956)
		Spell(PartySlot2,CLERIC_AID)
END

IF
	ActionListEmpty()
	HaveSpell(WIZARD_CHAOS_SHIELD)
	!CheckSpellState(Myself,CHAOS_SHIELD)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25956)
		Spell(Myself,WIZARD_CHAOS_SHIELD)
END

IF
	ActionListEmpty()
	HaveSpell(WIZARD_SHIELD)
	!CheckSpellState(Myself,WIZARD_SHIELD)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25956)
		Spell(Myself,WIZARD_SHIELD)
END

IF
	ActionListEmpty()
	HaveSpell(WIZARD_MINOR_SPELL_DEFLECTION)
	!CheckStat(Myself,1,WIZARD_SPELL_DEFLECTION)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25956)
		Spell(Myself,WIZARD_MINOR_SPELL_DEFLECTION)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_SHILLELAGH)  // SPPR110.SPL (Shillelagh)
	!HasItem("FBLADE",Myself)  // Flame Blade
	!HasItem("SHILLE",Myself)  // Shillelagh
	!HasItem("SHAMMR",Myself)  // Spiritual Hammer
	!HasItem("CHILLT",Myself)  // Chill Touch
	!HasItem("GHOULT",Myself)  // Ghoul Touch
	!HasItem("SGRASP",Myself)  // Shocking Grasp
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25956)
		Spell(Myself,CLERIC_SHILLELAGH)  // SPPR110.SPL (Shillelagh)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_FLAME_BLADE)
	!HasItem("FBLADE",Myself)  // Flame Blade
	!HasItem("SHILLE",Myself)  // Shillelagh
	!HasItem("SHAMMR",Myself)  // Spiritual Hammer
	!HasItem("CHILLT",Myself)  // Chill Touch
	!HasItem("GHOULT",Myself)  // Ghoul Touch
	!HasItem("SGRASP",Myself)  // Shocking Grasp
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25956)
		Spell(Myself,CLERIC_FLAME_BLADE)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_DRAW_UPON_HOLY_MIGHT)
	!CheckSpellState(Myself,DRAW_UPON_HOLY_MIGHT)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25956)
		Spell(Myself,CLERIC_DRAW_UPON_HOLY_MIGHT)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_ARMOR_OF_FAITH)
	!CheckSpellState(Myself,ARMOR_OF_FAITH)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25956)
		Spell(Myself,CLERIC_ARMOR_OF_FAITH)
END

IF
	ActionListEmpty()
	HaveSpell(WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
	OR(2)
		See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
		See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
	!CheckStat(Myself,1,MINORGLOBE)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25956)
		Spell(Myself,WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
END

IF
	ActionListEmpty()
	HaveSpell(WIZARD_RESIST_FEAR)
	!CheckSpellState(Myself,RESIST_FEAR)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25956)
		Spell(Myself,WIZARD_RESIST_FEAR)
END

// Attack spells
IF
	ActionListEmpty()
	HaveSpell(WIZARD_MAGIC_MISSILE)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,8624)
		Spell(NearestEnemyOf(Myself),WIZARD_MAGIC_MISSILE)
END

IF
	ActionListEmpty()
	HaveSpell(WIZARD_FLAME_ARROW)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		DisplayStringHead(Myself,8624)
		Spell(LastSeenBy(Myself),WIZARD_FLAME_ARROW)
END

IF
	ActionListEmpty()
	HaveSpell(WIZARD_AGANNAZAR_SCORCHER)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		DisplayStringHead(Myself,8624)
		Spell(LastSeenBy(Myself),WIZARD_AGANNAZAR_SCORCHER)
END

// Help hurt allies
IF
	ActionListEmpty()
	HPPercentLT(MostDamagedOf(Myself),80)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,578)
		AttackOneRound(LastAttackerOf(MostDamagedOf(Myself)))
END

// If attacked by an enemy attack them
IF 
	ActionListEmpty() 
	AttackedBy([ANYONE],DEFAULT)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,578)
		AttackOneRound(LastAttackerOf(Myself))
END

// Attack weakest enemy
IF
	ActionListEmpty()
	HPPercentLT(NearestEnemyOf(Myself),50)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,578)
		AttackOneRound(NearestEnemyOf(Myself))
END

IF
	ActionListEmpty()
	HPPercentLT(SecondNearestEnemyOf(Myself),50)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,578)
		AttackOneRound(SecondNearestEnemyOf(Myself))
END

IF
	ActionListEmpty()
	HPPercentLT(ThirdNearestEnemyOf(Myself),50)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,578)
		AttackOneRound(ThirdNearestEnemyOf(Myself))
END

//If there are nearby enemies, attack them
IF
	ActionListEmpty()
	NumCreatureVsPartyGT(Myself,0)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,578)
		AttackOneRound(NearestEnemyOf(Myself))
END

// Regroup if far away from each other
IF
	ActionListEmpty()
	!Range(PartySlot2,20)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,68344)
		MoveToObjectFollow(PartySlot2)
END

// Find traps
IF
	ActionListEmpty()
	Class(Myself,THIEF_ALL)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,9462)
		FindTraps()
END

// Randomly turn around, makes them look more alive
IF
	ActionListEmpty()
THEN
	RESPONSE #100
		DisplayStringHead(Myself,1539)
		RandomTurn()
END