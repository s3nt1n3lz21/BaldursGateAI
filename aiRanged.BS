// If ally health is low and have heal spell, heal them
IF
	ActionListEmpty()
	HaveSpell(CLERIC_CURE_LIGHT_WOUNDS)
	HPPercentLT(MostDamagedOf(Myself),80)
THEN
	RESPONSE #100
		Spell(MostDamagedOf(Myself),CLERIC_CURE_LIGHT_WOUNDS)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_CURE_MEDIUM_WOUNDS)
	HPPercentLT(MostDamagedOf(Myself),80)
THEN
	RESPONSE #100
		Spell(MostDamagedOf(Myself),CLERIC_CURE_MEDIUM_WOUNDS)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_CURE_SERIOUS_WOUNDS)
	HPPercentLT(MostDamagedOf(Myself),75)
THEN
	RESPONSE #100
		Spell(MostDamagedOf(Myself),CLERIC_CURE_SERIOUS_WOUNDS)
END

// If health is low use a healing potion
IF 
	ActionListEmpty()
	HPPercentLT(Myself,30) // If health is low use a potion
	HasItem("potn08",Myself)  // Potion of Healing
THEN
	RESPONSE #100
		UseItem("potn08",Myself)  // Potion of Healing
END

IF
	ActionListEmpty()
	HPPercentLT(Myself,30)
	HasItem("potn52",Myself)  // Potion of Extra Healing
THEN
	RESPONSE #100
		UseItem("potn52",Myself)  // Potion of Extra Healing
END

// If poisoned use an antidote potion
IF 
	ActionListEmpty()
	StateCheck(Myself,STATE_POISONED) 
	HasItem("potn20",Myself)  // Antidote
THEN
	RESPONSE #100
		UseItem("potn20",Myself)  // Antidote
END

// Run away from nearby enemies
IF 
	ActionListEmpty()
	See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),10)
THEN
	RESPONSE #100
		RunAwayFrom(NearestEnemyOf(Myself),25)
END

// Debuff spells
IF
	ActionListEmpty()
	HaveSpell(CLERIC_SILENCE_15_FOOT)  // SPPR211.SPL (Silence, 15' Radius)
	OR(2)
		See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
		See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
	Range(LastSeenBy(Myself),100)
	!StateCheck(LastSeenBy(Myself),STATE_SILENCED)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_SILENCE_15_FOOT)  // SPPR211.SPL (Silence, 15' Radius)
END

IF
	ActionListEmpty()
	HaveSpell(WIZARD_HORROR)
	NumCreatureVsPartyGT(Myself,4)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),WIZARD_HORROR)
END

// Summon spells
IF
	ActionListEmpty()
	HaveSpell(CLERIC_ANIMATE_DEAD)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_ANIMATE_DEAD)
END

// If have a buff spell use it
IF
	ActionListEmpty()
	HaveSpell(CLERIC_BLESS)
	!StateCheck(Myself,STATE_BLESS)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_BLESS)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_CHANT)
	!StateCheck(Myself,STATE_CHANT)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_CHANT)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_PROTECT_FROM_EVIL)
	!CheckSpellState(PartySlot2,PROTECTION_FROM_EVIL)
THEN
	RESPONSE #100
		Spell(PartySlot2,CLERIC_PROTECT_FROM_EVIL)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_BARKSKIN)
	!CheckSpellState(PartySlot2,BARKSKIN)
THEN
	RESPONSE #100
		Spell(PartySlot2,CLERIC_BARKSKIN)
END

IF
	ActionListEmpty()
	HaveSpell(WIZARD_CHAOS_SHIELD)
	!CheckSpellState(Myself,CHAOS_SHIELD)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_CHAOS_SHIELD)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_SHILLELAGH)  // SPPR110.SPL (Shillelagh)
	!HasItem("FBLADE",Myself)  // Flame Blade
	!HasItem("SHILLE",Myself)  // Shillelagh
	!HasItem("SHAMMR",Myself)  // Spiritual Hammer
	!HasItem("CHILLT",Myself)  // Chill Touch
	!HasItem("GHOULT",Myself)  // Ghoul Touch
	!HasItem("SGRASP",Myself)  // Shocking Grasp
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_SHILLELAGH)  // SPPR110.SPL (Shillelagh)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_FLAME_BLADE)
	!HasItem("FBLADE",Myself)  // Flame Blade
	!HasItem("SHILLE",Myself)  // Shillelagh
	!HasItem("SHAMMR",Myself)  // Spiritual Hammer
	!HasItem("CHILLT",Myself)  // Chill Touch
	!HasItem("GHOULT",Myself)  // Ghoul Touch
	!HasItem("SGRASP",Myself)  // Shocking Grasp
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_FLAME_BLADE)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_DRAW_UPON_HOLY_MIGHT)
	!CheckSpellState(Myself,DRAW_UPON_HOLY_MIGHT)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_DRAW_UPON_HOLY_MIGHT)
END

IF
	ActionListEmpty()
	HaveSpell(CLERIC_ARMOR_OF_FAITH)
	!CheckSpellState(Myself,ARMOR_OF_FAITH)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_ARMOR_OF_FAITH)
END

// Attack spells
IF
	ActionListEmpty()
	HaveSpell(WIZARD_MAGIC_MISSILE)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),WIZARD_MAGIC_MISSILE)
END

IF
	ActionListEmpty()
	HaveSpell(WIZARD_AGANNAZAR_SCORCHER)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),WIZARD_AGANNAZAR_SCORCHER)
END

// If attacked by enemy and they are far away, move towards them
IF 
	ActionListEmpty() 
	AttackedBy([ANYONE],DEFAULT)
	See(LastAttackerOf(Myself))
	!Range(LastAttackerOf(Myself),5)
	!InWeaponRange(LastAttackerOf(Myself))
THEN
	RESPONSE #100
		MoveToObject(LastAttackerOf(Myself))
END

//If attacked by enemy and enemy is nearby, attack them
IF
	ActionListEmpty()
	AttackedBy([ANYONE],DEFAULT)
	See(LastAttackerOf(Myself))
	OR(2)
		Range(LastAttackerOf(Myself),5)
		InWeaponRange(LastAttackerOf(Myself))
THEN
	RESPONSE #100
		AttackReevaluate(LastAttackerOf(Myself),30)
END

//If you can see a nearby enemy and they are in weapon range, attack them
IF
	ActionListEmpty()
	See(NearestEnemyOf(Myself))
	InWeaponRange(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),30)
END

// If you can see an enemy and they are not in range, move towards them
IF
	ActionListEmpty()
	See(NearestEnemyOf(Myself))
	!InWeaponRange(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		MoveToObject(NearestEnemyOf(Myself))
END

IF
	ActionListEmpty()
	!Range(PartySlot2,10)
THEN
	RESPONSE #100
		MoveToObject(PartySlot2)
END

